plugins {
    id 'java-library'
    id 'org.openapi.generator' version '5.3.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':notifications-management:application')

    implementation 'org.springframework:spring-web'
    implementation 'org.springframework:spring-context'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.1'

    implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.2'

    implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.2'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/openapi.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.example.modulith.demo.notifications.management.api.spring.web"
    invokerPackage = "com.example.modulith.demo.notifications.management"
    modelPackage = "com.example.modulith.demo.notifications.management.api.model"
    modelNameSuffix = "DTO"
    configOptions = [
      dateLibrary  : "java8",
      interfaceOnly: "true",
      useTags      : "true",
    ]
}

tasks.openApiGenerate {
    doLast {
        delete(
          "$buildDir/generated/pom.xml"
        )
    }
}

compileJava.dependsOn tasks.openApiGenerate
processResources.dependsOn tasks.openApiGenerate
sourceSets.main.java.srcDir "$buildDir/generated/src/main/java"
sourceSets.main.resources.srcDir "$buildDir/generated/src/main/resources"

tasks.register('modelJar', Jar) {
    from("$buildDir/classes/java/main") {
        include "com/example/modulith/demo/notifications/management/api/model/**"
    }
    archiveBaseName.set('api-model')
}

modelJar.dependsOn compileJava
jar.dependsOn modelJar

artifacts {
    archives modelJar
}
